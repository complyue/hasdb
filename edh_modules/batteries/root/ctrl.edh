
method error (***apk) throw EvalError( ***apk, unwind=2 )

quitShell := nil  # define the command to quit from a shell

{#
 # run a shell within an object's (probably a module) scope, e.g.
```edh
case { import (**_) 'batteries/magic' } of { m } -> shellWith(m)
```
 #}
method shellWith( obj ) {
  # use scope obj's repr for ps1
  cmdArgs = pkargs( ps1=''++obj++' Đ: ', ps2="Đ| ", inScopeOf=obj )

  console.print(' * entering a shell within ' ++ obj )
  console.print(' * key in `quitShell` to leave the shell.' )

  while true {

    case console.readCommand(***cmdArgs) of {
      quitShell -> { break }
      { cmd } -> {
        console.print( cmd )
        cmd = nil  # clear it
      }
    }

  } $=> {  # catch exceptions

    {{ ProgramHalt: ph }} -> {
      throw ph  # escalate a halt to the program loop of the
      # host interpreter, let it decide what to do.
    }

    {{ IOError:exc }} -> {
      console.fatal <| 'IO error - ' ++ exc
      throw exc  # escalate it to the program loop of the
      # host interpreter, let it decide what to do.
    }

    { exc } -> {
      console.print( 'Recovered from error', exception=exc )
    }

  }

}
